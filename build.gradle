plugins {
	id 'com.palantir.git-version' version '0.12.3'
	id 'org.springframework.boot' version '2.4.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'com.github.idegtiarenko'
version = getArtifactVersion()
sourceCompatibility = '11'

private def getArtifactVersion() {
	def artifactVersion = "1.0.0-SNAPSHOT"
	if (!artifactVersion.contains("SNAPSHOT")) {
		return artifactVersion
	}
	def details = versionDetails()
	return artifactVersion.replace("SNAPSHOT", details.gitHash + (details.isCleanTag ? "" : "-dirty"))
}

task getArtifactVersion {
	doLast {
		println getArtifactVersion()
	}
}

springBoot {
	buildInfo()
}

bootJar {
	manifest {
		attributes 'Implementation-Version': archiveVersion
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.springframework.boot:spring-boot-starter-web'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}
